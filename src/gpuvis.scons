#!python

import os

Import( 'env' )

if env[ 'buildtarget' ] == 'Lnx64' or env[ 'buildtarget' ] == 'Lnx32':
    env.Append( LIBS = [ 'm', 'dl', 'pthread' ] )
    env.Append( CXXFLAGS = [ '-fno-rtti', '-Woverloaded-virtual' ] )

    env.ParseConfig( 'sdl2-config --cflags --libs' )

    if env[ 'freetype' ]:
        env.Append( CPPDEFINES = [ 'USE_FREETYPE' ] )
        env.ParseConfig( 'pkg-config --cflags --libs freetype2' )

    if env[ 'gtk3' ]:
        env.Append( CPPDEFINES = [ 'USE_GTK3' ] )
        env.ParseConfig( 'pkg-config --cflags --libs gtk+-3.0' )

elif env[ 'buildtarget' ] == 'Win32' or env[ 'buildtarget' ] == 'Win64':
    env.Append( CPPFLAGS = [ '/GR-', '/EHsc', '/MT' ] )
    env.Append( CPPFLAGS = [ '/wd4996', '/wd4244', '/wd4090', '/wd4018', '/wd4267' ] )

    sdl2dir = os.environ['SDL2DIR']
    env.Append( CPPPATH = [ sdl2dir + "/include" ] )
    env.Append( LIBPATH = [ sdl2dir + "/lib/x64" ] )

    env.Append( LIBS = [ 'shlwapi', 'opengl32', 'comctl32', 'comdlg32', 'SDL2' ] )

sources = [
    'gpuvis.cpp',
    'gpuvis_graph.cpp',
    'gpuvis_framemarkers.cpp',
    'gpuvis_plots.cpp',
    'gpuvis_graphrows.cpp',
    'gpuvis_ftrace_print.cpp',
    'gpuvis_utils.cpp',
    'tdopexpr.cpp',
    'ya_getopt.c',
    'MurmurHash3.cpp',
    'miniz.c',
    'stlini.cpp',
    'imgui/imgui_impl_sdl_gl3.cpp',
    'imgui/imgui.cpp',
    'imgui/imgui_demo.cpp',
    'imgui/imgui_draw.cpp',
    'imgui/imgui_freetype.cpp',
    'GL/gl3w.c',
    'trace-cmd/event-parse.c',
    'trace-cmd/trace-seq.c',
    'trace-cmd/kbuffer-parse.c',
    'trace-cmd/trace-read.cpp'
    ]

env.Program( 'gpuvis', source = sources )
